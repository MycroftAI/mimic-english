project('mimic-english', 'c', default_options : ['c_std=c99'], version: '0')

build_plugin = get_option('plugin')

cmulex_src = [
  'src/cmulex/cmu_lex.c',
  'src/cmulex/cmu_lex_data.c',
  'src/cmulex/cmu_lex_entries.c',
  'src/cmulex/cmu_lts_model.c',
  'src/cmulex/cmu_lts_model.h',
  'src/cmulex/cmu_lts_rules.c',
  'src/cmulex/cmu_postlex.c']

usenglish_src = [
  'src/usenglish/us_aswd.c',
  'src/usenglish/us_dur_stats.c',
  'src/usenglish/us_durz_cart.c',
  'src/usenglish/usenglish.c',
  'src/usenglish/us_expand.c',
  'src/usenglish/us_f0lr.c',
  'src/usenglish/us_f0_model.c',
  'src/usenglish/us_ffeatures.c',
  'src/usenglish/us_gpos.c',
  'src/usenglish/us_int_accent_cart.c',
  'src/usenglish/us_int_tone_cart.c',
  'src/usenglish/us_nums_cart.c',
  'src/usenglish/us_phoneset.c',
  'src/usenglish/us_phrasing_cart.c',
  'src/usenglish/us_pos_cart.c',
  'src/usenglish/us_text.c']

sources = usenglish_src + cmulex_src

includes = [include_directories('include')]

# Core Dependency:
mimiccore = dependency('ttsmimiccore')
mimic_plugin_dir = mimiccore.get_pkgconfig_variable('mimic_plugin_dir')

# If mimic-core does not have plugin support, disable the plugin
if mimic_plugin_dir == ''
    build_plugin = false
endif
message('Build mimic-english plugin: @0@'.format(build_plugin))


en_us_header_subdir = join_paths('ttsmimic', 'lang', 'en_US')

conf_data = configuration_data()
conf_data.set('ENABLE_LANG_ENGLISH_BUILTIN', not build_plugin)

mimic_english_config_h = configure_file(output : 'mimic_english_config.h',
                                        configuration : conf_data, install: true,
                                        install_dir : join_paths(get_option('includedir'), en_us_header_subdir))
sources += [mimic_english_config_h]

ttsmimic_english_lib = library('ttsmimic_english', sources,
                               include_directories : includes,
                               dependencies: mimiccore,
                               install: true)

ttsmimiclib_english_dep = declare_dependency(link_with : ttsmimic_english_lib, 
                                             include_directories : includes)


if build_plugin
  ttsmimic_english_plugin_lib = library(
    'ttsmimic_english_plugin',
    ['src/mimic_english_plugin.c'],
    include_directories : includes,
    dependencies: [mimiccore, ttsmimiclib_english_dep],
    install_dir: mimic_plugin_dir,
    install: true)

  ttsmimiclib_english_plugin_dep = declare_dependency(
    link_with : ttsmimic_english_plugin_lib, 
    include_directories : includes)
endif



############ Headers #################

ttsmimic_headers = ['include/cmu_lex.h', 'include/usenglish.h']

install_headers(ttsmimic_headers, subdir : en_us_header_subdir)

# FIXME: Some files were autogenerated from festival sources.
# It would be cool to regenerate them from sources easily.
#regexes:
#	./make_us_regexes $(BINDIR) >us_regexes.h
#
#us_pos_cart.c: 
#	$(ESTDIR)/../festival/bin/festival \
#           -b $(MIMICDIR)/tools/make_cart.scm \
#               $(MIMICDIR)/tools/make_pos.scm 
# src/cmulex/make_cmulex_helper.py
#  'src/cmulex/make_cmulex',
#  'src/cmulex/cmu_lts_model.h',
#  'src/usenglish/us_pos.tree',
#  'src/usenglish/us_regexes.h',
#  'src/usenglish/us_ffeatures.h',
#  'src/usenglish/us_f0.h',
#  'src/usenglish/us_int_tone_cart.h',
#  'src/usenglish/us_int_accent_cart.h',
#  'src/usenglish/us_phrasing_cart.h',
#  'src/usenglish/us_nums_cart.h',
#  'src/usenglish/us_durz_cart.h',
#  'src/usenglish/us_pos_cart.h',

########### pkg-config #############

pkg = import('pkgconfig')

pkg.generate(libraries : ttsmimic_english_lib,
             name : 'ttsmimicenglish',
             subdirs: en_us_header_subdir,
             requires: 'ttsmimiccore',
             description: 'English support for text to speech synthesis in mimic.',
             version: meson.project_version())

