project('mimic-english', 'c', default_options : ['c_std=c99'], version: '0')

mimic_core_deps = []
c_compiler = meson.get_compiler('c')
host_system = host_machine.system()

conf_data = configuration_data()

build_as_plugin = get_option('plugin')
message('Build mimic-english as plugin: @0@'.format(build_as_plugin))

cmulex_src = [
  'src/cmulex/cmu_lex.c',
  'src/cmulex/cmu_lex_data.c',
  'src/cmulex/cmu_lex_entries.c',
  'src/cmulex/cmu_lts_model.c',
  'src/cmulex/cmu_lts_model.h',
  'src/cmulex/cmu_lts_rules.c',
  'src/cmulex/cmu_postlex.c']

usenglish_src = [
  'src/usenglish/us_aswd.c',
  'src/usenglish/us_dur_stats.c',
  'src/usenglish/us_durz_cart.c',
  'src/usenglish/usenglish.c',
  'src/usenglish/us_expand.c',
  'src/usenglish/us_f0lr.c',
  'src/usenglish/us_f0_model.c',
  'src/usenglish/us_ffeatures.c',
  'src/usenglish/us_gpos.c',
  'src/usenglish/us_int_accent_cart.c',
  'src/usenglish/us_int_tone_cart.c',
  'src/usenglish/us_nums_cart.c',
  'src/usenglish/us_phoneset.c',
  'src/usenglish/us_phrasing_cart.c',
  'src/usenglish/us_pos_cart.c',
  'src/usenglish/us_text.c']

sources = usenglish_src + cmulex_src + ['src/mimic_english_plugin.c']


includes = [include_directories('include')]

mimiccore = dependency('ttsmimiccore', required: false)
if mimiccore.found()
  mimic_plugin_dir = mimiccore.get_pkgconfig_variable('mimic_plugin_dir')
else
  # set up mimic-core using an external dependency
  mimic_core_pr = subproject('mimic-core')
  mimiccore = mimic_core_pr.get_variable('ttsmimiccore_dep')
  mimic_plugin_dir = mimic_core_pr.get_variable('mimic_plugin_dir')
  includes += include_directories('subprojects/mimic-core/')
endif

# If mimic-core does not have plugin support, disable the plugin
if mimic_plugin_dir == ''
    build_as_plugin = false
endif

conf_data.set('ENABLE_LANG_ENGLISH_BUILTIN', !build_as_plugin)


if build_as_plugin
 lib_install_to = mimic_plugin_dir
else
 lib_install_to = join_paths(get_option('prefix'), get_option('libdir'))
endif

ttsmimic_english_lib = library('ttsmimic_english', sources,
                               include_directories : includes,
                               dependencies: mimiccore,
                               install_dir: lib_install_to,
                               install: true)

ttsmimiclib_english_dep = declare_dependency(link_with : ttsmimic_english_lib, 
                                             include_directories : includes)


############ Headers #################

ttsmimic_headers = ['include/cmu_lex.h', 'include/usenglish.h']

install_headers(ttsmimic_headers, subdir : join_paths('ttsmimic', 'lang', 'en_US'))

# FIXME: Some files were autogenerated from festival sources.
# It would be cool to regenerate them from sources easily.
#regexes:
#	./make_us_regexes $(BINDIR) >us_regexes.h
#
#us_pos_cart.c: 
#	$(ESTDIR)/../festival/bin/festival \
#           -b $(MIMICDIR)/tools/make_cart.scm \
#               $(MIMICDIR)/tools/make_pos.scm 
# src/cmulex/make_cmulex_helper.py
#  'src/cmulex/make_cmulex',
#  'src/cmulex/cmu_lts_model.h',
#  'src/usenglish/us_pos.tree',
#  'src/usenglish/us_regexes.h',
#  'src/usenglish/us_ffeatures.h',
#  'src/usenglish/us_f0.h',
#  'src/usenglish/us_int_tone_cart.h',
#  'src/usenglish/us_int_accent_cart.h',
#  'src/usenglish/us_phrasing_cart.h',
#  'src/usenglish/us_nums_cart.h',
#  'src/usenglish/us_durz_cart.h',
#  'src/usenglish/us_pos_cart.h',

########### pkg-config #############

pkg = import('pkgconfig')

pkg.generate(libraries : ttsmimic_english_lib,
             name : 'ttsmimicenglish',
             description: 'English support for text to speech synthesis in mimic.',
             version: meson.project_version())

